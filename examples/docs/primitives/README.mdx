---
sidebar_position: -1
marp: true
hide_table_of_contents: true
title: 'Primitives'
description: 'Primitives'
image: https://github.com/tseijp.png
keywords:
        [
                glsl,
                webgl,
                hooks,
                react,
                reactjs,
                reactive,
                solid,
                solidjs,
                typescript,
        ]
date: 2023-01-01
---

# Primitives

<Canvas />

## More

https://iquilezles.org/articles/distfunctions/

export const frag = `
precision highp float;
uniform vec2 iResolution; // canvas size
uniform vec3 up;          // camera up direction
uniform vec3 eye;         // camera position
uniform vec3 focus;       // camera focus point
uniform float focal;      // camera focal length
uniform float repeat;     // repeat space
uniform float sphereRadius;
uniform vec3 boxSize;
uniform vec3 roundBoxSize;
uniform float roundBoxRound;
uniform vec3 boxFrameSize;
uniform float boxFrameFrame;
uniform float torusRadius;
uniform float torusTube;
uniform float coneRadius;
uniform float coneHeight;
uniform vec3 planeNormal;
uniform float planeDistance;
uniform vec3 capsuleLeft;
uniform vec3 capsuleRight;
uniform float capsuleRadius;
uniform float cylinderHeight;
uniform float cylinderRadius;
uniform float octahedronSize;
uniform float circleRadius;
uniform float dodecahedronSize;
uniform float icosahedronSize;
uniform float truncatedOctahedronSize;
uniform float truncatedIcosahedronSize;
/**
 * SDF
 */
#define PHI (1.618033988749895)
#define _0 vec3(1, 0, 0)
#define _1 vec3(0, 1, 0)
#define _2 vec3(0, 0, 1)
#define _3 normalize(vec3(1, 1, 1))
#define _4 normalize(vec3(-1, 1, 1))
#define _5 normalize(vec3(1, -1, 1))
#define _6 normalize(vec3(1, 1, -1))
#define _7 normalize(vec3(0, 1, PHI + 1.))
#define _8 normalize(vec3(0, -1, PHI + 1.))
#define _9 normalize(vec3(PHI + 1., 0, 1))
#define _10 normalize(vec3(-PHI - 1., 0, 1))
#define _11 normalize(vec3(1, PHI + 1., 0))
#define _12 normalize(vec3(-1, PHI + 1., 0))
#define _13 normalize(vec3(0, PHI, 1))
#define _14 normalize(vec3(0, -PHI, 1))
#define _15 normalize(vec3(1, 0, PHI))
#define _16 normalize(vec3(-1, 0, PHI))
#define _17 normalize(vec3(PHI, 1, 0))
#define _18 normalize(vec3(-PHI, 1, 0))
#define _(v) d = max(d, abs(dot(p, v)));
float sphereSDF(vec3 p, float s) {
        return length(p) - s;
}
float boxSDF(vec3 p, vec3 b) {
        vec3 q = abs(p) - b;
        return length(max(q, 0.)) + min(max(q.x, max(q.y, q.z)), 0.);
}
float roundBoxSDF(vec3 p, vec3 b, float r) {
        vec3 q = abs(p) - b + r;
        return length(max(q, 0.0)) + min(max(q.x, max(q.y, q.z)), 0.0) - r;
}
#define len(x, y, z) length(max(vec3(x, y, z), 0.)) + min(max(x, max(y, z)), 0.)
float boxFrameSDF(vec3 p, vec3 b, float e) {
        p = abs(p) - b;
        vec3 q = abs(p + e) - e;
        return min(min(len(p.x, q.y, q.z), len(q.x, p.y, q.z)), len(q.x, q.y, p.z));
}
float torusSDF(vec3 p, float r, float t) {
        vec2 q = vec2(length(p.yz) - r + t, p.x);
        return length(q) - t;
}
float coneSDF(vec3 p, float r, float h) {
	vec2 q = vec2(length(p.xz), p.y + h * 0.5);
	vec2 tip = q - vec2(0, h);
	vec2 dir = normalize(vec2(h, r));
	float d = max(dot(tip, dir), -q.y);
	float e = dot(tip, vec2(dir.y, -dir.x));
	if ((q.y > h) && (e < 0.)) d = max(d, length(tip));
	if ((q.x > r) && (e > length(vec2(h, r))))
		d = max(d, length(q - vec2(r, 0)));
	return d;
}
float planeSDF(vec3 p, vec3 n, float d) {
	return dot(p, n) + d;
}
float capsuleSDF(vec3 p, vec3 a, vec3 b, float r) {
        vec3 pa = p - a, ba = b - a;
        float h = clamp(dot(pa, ba) / dot(ba, ba), 0., 1.);
        return length(pa - ba * h) - r;
}
float cylinderSDF(vec3 p, float h, float r) {
        vec2 d = abs(vec2(length(p.xz), p.y)) - vec2(r, h);
        return min(max(d.x, d.y), 0.) + length(max(d, 0.));
}
float octahedronSDF(vec3 p, float r) {
        float d = 0.;
        _(_3) _(_4) _(_5) _(_6)
        return d - r;
}
float circleSDF(vec3 p, float r) {
        float l = length(p.xy) - r;
	return l < 0. ? abs(p.z) : length(vec2(p.z, l));
}
float dodecahedronSDF(vec3 p, float r) {
        float d = 0.;
        _(_13) _(_14) _(_15) _(_16)
        _(_17) _(_18)
        return d - r;
}
float icosahedronSDF(vec3 p, float r) {
        float d = 0.;
        _(_3) _(_4) _(_5) _(_6)
        _(_7) _(_8) _(_9) _(_10)
        _(_11) _(_12)
        return d - r;
}
float truncatedOctahedronSDF(vec3 p, float r) {
        float d = 0.;
        _(_0) _(_1) _(_2)
        _(_3) _(_4) _(_5) _(_6)
        return d - r;
}
float truncatedIcosahedronSDF(vec3 p, float r) {
        float d = 0.;
        _(_3) _(_4) _(_5) _(_6)
        _(_7) _(_8) _(_9) _(_10)
        _(_11) _(_12) _(_13) _(_14)
        _(_15) _(_16) _(_17) _(_18)
        return d - r;
}float truncatedXXXSDF(vec3 p, float r) {
        float d = 0.;
        _(_0) _(_1) _(_2)
        _(_7) _(_8) _(_9) _(_10) _(_11) _(_12)
        return d - r;
}float truncatedYYYSDF(vec3 p, float r) {
        float d = 0.;
        _(_0) _(_1) _(_2)
        _(_7) _(_8) _(_9) _(_10) _(_11) _(_12)
        _(_13) _(_14) _(_15) _(_16) _(_17) _(_18)
        return d - r;
}
#define _min(x) d = min(d, x);
#define gap(x, y) vec3(x * 40., 0., y * 40.)
float SDF(vec3 p) {
        float d = 1e10;
        _min(              sphereSDF(p + gap(-3., -3.), sphereRadius))
        _min(                 boxSDF(p + gap(-3., -1.), boxSize))
        _min(            roundBoxSDF(p + gap(-3., 1.), roundBoxSize, roundBoxRound))
        _min(            boxFrameSDF(p + gap(-3., 3.), boxFrameSize, boxFrameFrame))
        _min(               torusSDF(p + gap(-1., -3.), torusRadius, torusTube))
        _min(                coneSDF(p + gap(-1., -1.), coneRadius, coneHeight))
        _min(             capsuleSDF(p + gap(-1., 1.), capsuleLeft, capsuleRight, capsuleRadius))
        _min(            cylinderSDF(p + gap(-1., 3.), cylinderHeight, cylinderRadius))
        _min(          octahedronSDF(p + gap(1., -3.), octahedronSize))
        _min(              circleSDF(p + gap(1., -1.), circleRadius))
        _min(        dodecahedronSDF(p + gap(1., 1.), dodecahedronSize))
        _min(         icosahedronSDF(p + gap(1., 3.), icosahedronSize))
        _min( truncatedOctahedronSDF(p + gap(3., -3.), truncatedOctahedronSize))
        _min(truncatedIcosahedronSDF(p + gap(3., -1.), truncatedIcosahedronSize))
        _min(        truncatedXXXSDF(p + gap(3., 1.), truncatedIcosahedronSize))
        _min(        truncatedYYYSDF(p + gap(3., 3.), truncatedIcosahedronSize))
        // _min(planeSDF(p, planeNormal, planeDistance))
        return d;
}
/**
 * main
 */
void main() {
        vec3 look = normalize(focus - eye);
        vec3 right = normalize(cross(look, up));
        vec2 scr = gl_FragCoord.xy - iResolution * .5;
        vec3 dir = normalize(focal * look + scr.x * right + scr.y * up);
        /**
         * Ray marching
         */
        vec3 p = eye + dir;
        vec3 e = vec3(.0005, 0., 0.);
        float d = SDF(p);
        for (int i = 0; i < 100; i++) {
                if (d <= e.x) {
                        float dx = SDF(p + e.xyy) - d;
                        float dy = SDF(p + e.yxy) - d;
                        float dz = SDF(p + e.yyx) - d;
                        vec3 norm = normalize(vec3(dx, dy, dz));
                        gl_FragColor = vec4(norm * .5 + .5, 1.);
                        return;
                }
                p = p + d * dir;
                d = SDF(p);
        }
}`

import { Leva } from 'leva'
import {
        useGLRender,
        useResizeRef,
        useOrbitControls,
        useUniformControls,
} from './hooks'

export const Canvas = () => {
        const self = useGLRender(frag)
        const ref = useResizeRef(self)
        useOrbitControls('eye', self, 100)
        useUniformControls(
                {
                        sphereRadius: { value: 50, min: 0, max: 100 },
                        boxSize: [32, 32, 32],
                        roundBoxSize: [32, 32, 32],
                        roundBoxRound: { value: 20, min: 0, max: 40 },
                        boxFrameSize: [32, 32, 32],
                        boxFrameFrame: { value: 2, min: 0, max: 15 },
                        torusRadius: { value: 40, min: 0, max: 80 },
                        torusTube: { value: 12, min: 0, max: 100 },
                        coneRadius: { value: 20, min: 0, max: 40 },
                        coneHeight: { value: 40, min: 0, max: 80 },
                        planeNormal: [0, 1, 0],
                        planeDistance: { value: 5, min: 0, max: 10 },
                        capsuleLeft: [-10, 0, 0],
                        capsuleRight: [10, 0, 0],
                        capsuleRadius: { value: 20, min: 0, max: 40 },
                        cylinderHeight: { value: 20, min: 0, max: 40 },
                        cylinderRadius: { value: 20, min: 0, max: 40 },
                        octahedronSize: { value: 20, min: 0, max: 40 },
                        circleRadius: { value: 20, min: 0, max: 40 },
                        dodecahedronSize: { value: 25, min: 0, max: 50 },
                        icosahedronSize: { value: 25, min: 0, max: 50 },
                        truncatedOctahedronSize: {
                                value: 25,
                                min: 0,
                                max: 50,
                        },
                        truncatedIcosahedronSize: {
                                value: 25,
                                min: 0,
                                max: 50,
                        },
                        focal: 500,
                        focus: [0, 0, 0],
                        up: [0, 1, 0],
                },
                self
        )
        return (
                <div ref={ref}>
                        <canvas ref={self.ref} />
                        <Leva />
                </div>
        )
}
